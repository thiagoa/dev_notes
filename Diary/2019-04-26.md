## 2019-04-25 - Lisp

[The last entry](https://github.com/thiagoa/notes/blob/master/Diary/2019-04-25.md) is relevant to understand this one.

Here's a function to divide a list of nodes and edges into islands
(i.e., a collection of node lists that are connected):

```lisp
;; Changed to be more functional
(defun find-islands (nodes edge-list)
  "Given a list of NODES and EDGE-LIST, find all the islands.

'Island' is a set of nodes connected through edges. Some nodes will simply
be isolated from the bigger island, and will constitute their own small
islands.

This function follows a familiar recursive pattern where we pass an
initial empty list (the nil value), which gets filled through
recursive calls. Example:

;; Returns ((5 4) (6) (3 2 1))
(find-islands '(1 2 3 4 5 6) '((1 . 2) (2 . 3) (4 . 5)))

The get-connected function returns a single island:

;; Returns (3 2 1)
(get-connected 1 '((1 . 2) (2 . 3) (4 . 5)))

After getting the island relative to the first NODES element,
we simply extract the set difference - the remaining
nodes that will constitute new islands - and call the function
recursively."
  (labels ((find-island (nodes islands)
             (let* ((connected (get-connected (car nodes) edge-list))
                    (unconnected (set-difference nodes connected)))
               (let ((islands (push connected islands)))
                 (if unconnected
                     (find-island unconnected islands)
                     islands)))))
    (find-island nodes nil)))
```

Now, the following function connects the islands together:

```lisp
(defun connect-with-bridges (islands)
  "Given a list of ISLANDS, this function returns the set of edges to connect them.

;; Returns ((1 . 8) (8 . 1))
(connect-with-bridges '((1 2 3) (8 9)))"
  (when (cdr islands)
    (append (edge-pair (caar islands) (caadr islands))
            (connect-with-bridges (cdr islands)))))

(defun edge-pair (a b)
  "Return a list of edges to connect nodes A and B bidirectionally."
  (unless (eql a b)
    (list (cons a b) (cons b a))))
```

When out of context, the name "connect-with-bridges" can be misleading because
it gives no clue about the return value.

Here's a good example of how both functions work together:

```lisp
;; Result of find-islands: ((5 4) (6) (3 2 1))
(let ((islands (find-islands '(1 2 3 4 5 6) '((1 . 2) (2 . 3) (4 . 5)))))
  ;; Result of connect-with-bridges: ((5 . 6) (6 . 5) (6 . 3) (3 . 6))
  (connect-with-bridges islands))
```

What's interesting about the `connect-with-bridges` function, is that
it can be built out of a higher-level pattern:

```lisp
(defun map-cons (func l)
  (labels ((call (l acc)
             (if (cdr l)
                 (progn (push (funcall func (car l) (cadr l)) acc)
                        (call (cdr l) acc))
                 acc)))
    (reverse (call l (list)))))

(defun connect-with-bridges (islands)
  (let ((edge-pairs (map-cons (lambda (a b) (edge-pair (car a) (car b)))
                              islands)))
    (apply 'append edge-pairs)))
```

It's the same LOC, but overall less complexity and `map-cons` does
tail call optimization, while the original `connect-with-bridges` does
not.
