## 2019-06-13 - Fancy Lisp macros

Let's discuss macro-writing-macros, starting with one the most useless
macros you've ever seen:

```lisp
(defmacro useless-macro-writing-macro (a b)
  (let ((vars (list (list 'a a) (list 'b b))))
    `(progn
       `(let ,(let ,vars (list (list 'a a) (list 'b b)))
          (concatenate 'string
                       "a is " (write-to-string a)
                       " and b is " (write-to-string b))))))
```

Can you guess what the output of this macro is? It will expand to:

```lisp
(PROGN
  `(LET ........
```

If we evaluate it, it will spit out the following quoted form:

```lisp
CL-USER> (useless-macro-writing-macro 1 3)
(LET ((A 1) (B 3))
  (CONCATENATE 'STRING "a is " (WRITE-TO-STRING A) " and b is "
               (WRITE-TO-STRING B)))
```

And this quoted form can then be evaluated:

```lisp
CL-USER> (eval (useless-macro-writing-macro 1 3))
"a is 1 and b is 3"
```

There are interesting things to note about the macro code. The
following expansion is under two levels of quoting:

```lisp
,(let ,vars (list (list 'a a) (list 'b b))
```

In the first expansion, `,vars` will get inserted into the macro
template right away. The reason is that at the point where `,vars` is
located, we have two syntax quotes (quotes) and two commas
(unquotes). This will force the expansion of `,vars` during the first
pass and we will get:

```lisp
CL-USER> (macroexpand-1 '(useless-macro-writing-macro 1 3))
(PROGN
 `(LET ,(LET ((A 1) (B 3))
          (LIST (LIST 'A A) (LIST 'B B)))
    (CONCATENATE 'STRING "a is " (WRITE-TO-STRING A) " and b is "
                 (WRITE-TO-STRING B))))
```

Let's see how that happens in detail, and recall that the following example can be executed outside of macros:

```lisp
CL-USER> (defparameter a 1)
A
CL-USER> `(,a) ;; quote + unquote
(1)
CL-USER> `(`(,a)) ;; quote the "quote + unquote"... oops.. "a" is quoted again
(`(,A))
CL-USER> `(`(,,a)) ;; force the expansion of "a" again with another unquote
(`(,1))
CL-USER> `(`,(,a)) ;; this also works.. still 2 quotes + 2 unquotes
(`,(1))
```

This is not very useful because it can't be eval'ed, as 1 is not a
function. How about this:

```lisp
CL-USER> (defparameter func 'princ)
FUNC
CL-USER> `(princ (concatenate 'string `,(,func "hello") " Joe"))
(PRINC (CONCATENATE 'STRING `,(PRINC "hello") " Joe"))
CL-USER> (eval `(princ (concatenate 'string `,(,func "hello") " Joe")))
hellohello Joe
"hello Joe"
```

We are expanding `func` into the `princ` function (forget the
side-effects of `princ` and focus on the return value instead),
and concatenating its return value with a string literal, which is
then sent to a hardcoded `princ`.

> Think of syntax quotes as installments. If we want to expand something
> from within two levels of syntax quoting, we have to pay 2 unquote
> installments.

What happens when we line up the two unquotes?

```lisp
CL-USER> `(princ (concatenate 'string `(,,func "hello") " Joe"))
(PRINC (CONCATENATE 'STRING `(,PRINC "hello") " Joe"))
CL-USER> (eval `(princ (concatenate 'string `(,,func "hello") " Joe")))
; Evaluation aborted on #<UNBOUND-VARIABLE PRINC {100579A393}>.
```

`func` is still expanded as you can see, but the expanded code won't
do what we want. We got "unbound variable princ" because we're trying
to expand a variable into a list, and not a function call
anymore. Defining a `princ` variable would solve the problem, but we
can't define this variable because it's special. Even if we set `func`
to "foo", it would still give an error: you can't concatenate a list
with a string.

The point of lining up the unquotes was to evidence the "installments"
principle because even still `func` gets expanded as expected.

### More details

Let's return to the first macro. When we execute it at the REPL, two
things happen:

```lisp
CL-USER> (useless-macro-writing-macro 1 3)
(LET ((A 1) (B 3))
  (CONCATENATE 'STRING "a is " (WRITE-TO-STRING A) " and b is "
               (WRITE-TO-STRING B)))
```

1. The macro is expanded
2. The code is executed

But of course, these are separate steps that are conflated by the REPL.

The above example is equivalent to:

```lisp
CL-USER>  (eval (macroexpand-1 '(useless-macro-writing-macro 1 3)))
(LET ((A 1) (B 3))
  (CONCATENATE 'STRING "a is " (WRITE-TO-STRING A) " and b is "
               (WRITE-TO-STRING B)))
```

What's the use of this macro if it returns a new Lisp form? The answer
is "macro-writing-macro". This macro is only useful when called from
within another macro. Let's see a super stupid example:

```lisp
(defmacro another-macro (a b)
  (useless-macro-writing-macro a b))
```

Which gives the final answer and no longer a Lisp form:

```
CL-USER> (another-macro 1 3)
"a is 1 and b is 3"
```

So what happens? Macros are recursive. The call to
`useless-macro-writing-macro` will get expanded into `another-macro`
when compiling `another-macro`. The latter will already have the
final code that we want to execute, by means of the macro.

### A useful example

A super useful macro-writing-macro is pictured in the book "Practical
Common Lisp":

```lisp
(defmacro once-only ((&rest names) &body body)
  (let ((gensyms (loop for n in names collect (gensym))))
    `(let (,@(loop for g in gensyms collect `(,g (gensym))))
       `(let (,,@(loop for g in gensyms for n in names collect ``(,,g ,,n)))
          ,(let (,@(loop for n in names for g in gensyms collect `(,n ,g)))
             ,@body)))))
```

It is supposed to be used by other macros and will evaluate whatever
variables you pass in just once, while leaving no trace of the
original values internally, and letting you reference them by their
original names. It is the same principle as the stupid
`useless-macro-writing-macro`. Let's see how it expands step by step:

```lisp
CL-USER> (defparameter a 1)
A
CL-USER> (defparameter b 2)
B
CL-USER> (macroexpand-1 '(once-only (a b) `(list a b)))
(LET ((#:G659 (GENSYM)) (#:G660 (GENSYM)))
  `(LET (,`(,#:G659 ,A) ,`(,#:G660 ,B))
     ,(LET ((A #:G659) (B #:G660))
        `(LIST A B))))
CL-USER> (eval (macroexpand-1 '(once-only (a b) `(list a b))))
(LET ((#:G663 1) (#:G664 2))
  (LIST A B))
CL-USER> (eval (eval (macroexpand-1 '(once-only (a b) `(list a b)))))
; in: LET ((#:G667 1) (#:G668 2))
;     (LET ((#:G667 1) (#:G668 2))
;       (LIST A B))
;
; caught STYLE-WARNING:
;   The variable #:G667 is defined but never used.
;
; caught STYLE-WARNING:
;   The variable #:G668 is defined but never used.
;
; compilation unit finished
;   caught 2 STYLE-WARNING conditions
(1 2)
```

It is even more useful when the variable points to an unevaluated Lisp
form, which is the case for macro arguments (unless the argument is a form that can't be expanded further):

```lisp
CL-USER> (defparameter a '(random 10))
A
CL-USER> (eval (eval (macroexpand-1 '(once-only (a b) `(list ,a b)))))
; in: LET ((#:G729 (RANDOM 10)) (#:G730 2))
;     (LET ((#:G729 (RANDOM 10)) (#:G730 2))
;       (LIST #:G729 B))
;
; caught STYLE-WARNING:
;   The variable #:G730 is defined but never used.
;
; compilation unit finished
;   caught 1 STYLE-WARNING condition
(7 2)
```
